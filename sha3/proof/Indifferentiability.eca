(** A primitive: the building block we assume ideal **)
type p_in, p_out.

module type Primitive = {
  proc init(): unit
  proc oracle(x : p_in): p_out
}.

(** A functionality: the target construction **)
type f_in, f_out.

module type Functionality = {
  proc init(): unit
  proc oracle(x : f_in): f_out
}.

(** A construction takes a primitive and builds a functionality.
    A simulator takes a functionality and simulates the primitive.
    A distinguisher gets oracle access to a primitive and a
      functionality and returns a boolean (its guess as to whether it
      is playing with constructed functionality and ideal primitive or
      with ideal functionality and simulated primitive). **)
module type Construction (P : Primitive) = {
  proc init()          : unit  {  P.init  }
  proc oracle(x : f_in): f_out { P.oracle }
}.

module type Simulator (F : Functionality) = {
  proc init()          : unit  {  F.init  }
  proc oracle(x : p_in): p_out { F.oracle }
}.

module type Distinguisher (F : Functionality, P : Primitive) = {
  proc distinguish(): bool { P.oracle F.oracle }
}.

module Indif (F : Functionality, P : Primitive, D : Distinguisher) = {
  module D = D(F,P)

  proc main(): bool = {
    var b;

    P.init();
    F.init();
    b = D.distinguish();
    return b;
  }
}.

(* (C <: Construction) applied to (P <: Primitive) is indifferentiable
   from (F <: Functionality) if there exists (S <: Simulator) such
   that, for all (D <: Distinguisher),
      | Pr[Indif(C(P),P,D): res] - Pr[Indif(F,S(F),D): res] | is small.
   We avoid the existential by providing a concrete construction for S
   and the `small` by providing a concrete bound. *)
