require import Core Int Real StdOrder Ring Distr IntExtra.
require import List FSet NewFMap Utils Common SLCommon DProd Dexcepted.

(*...*) import Capacity IntOrder RealOrder.

require (*..*) Strong_RP_RF.

module PF = {
  var m, mi: (state,state) fmap

  proc init(): unit = {
    m  <- map0;
    mi <- map0;
  }

  proc f(x : state): state = {
    var y1, y2;

    if (!mem (dom m) x) {
      y1           <$ bdistr;
      y2           <$ cdistr;
      m.[x]        <- (y1,y2);
      mi.[(y1,y2)] <- x;
    }
    return oget m.[x];
  }

  proc fi(x : state): state = {
    var y1, y2;

    if (!mem (dom mi) x) {
      y1          <$ bdistr;
      y2          <$ cdistr;
      mi.[x]      <- (y1,y2);
      m.[(y1,y2)] <- x;
    }
    return oget mi.[x];
  }

}.

module CF(D:DISTINGUISHER) = Indif(SqueezelessSponge(PF), PF, D).
     
section.
  declare module D : DISTINGUISHER {Perm, C, PF, Redo}.

  axiom D_ll (F <: DFUNCTIONALITY{D}) (P <: DPRIMITIVE{D}):
    islossless P.f => islossless P.fi => islossless F.f => 
    islossless D(F, P).distinguish.

  local module GReal' = Indif(FC(SqueezelessSponge(Perm)), PC(Perm), D).

  local clone import Strong_RP_RF as Switching with
    type D  <- state,
      op uD <- dstate,
    type K  <- unit,
      op dK <- (MUnit.dunit<:unit> tt),
      op q  <- max_size
  proof *.
  realize ge0_q by smt w=max_ge0.
  realize uD_uf_fu.
  split.
    case=> [x y]; rewrite supp_dprod /=.
    rewrite Block.DBlock.supp_dunifin Capacity.DCapacity.supp_dunifin/=. 
      smt(dprod1E Block.DBlock.dunifin_funi Capacity.DCapacity.dunifin_funi).
  split. 
    smt(dprod_ll  Block.DBlock.dunifin_ll Capacity.DCapacity.dunifin_ll).
  apply/dprod_fu.
    rewrite Block.DBlock.dunifin_fu.
  by rewrite Capacity.DCapacity.dunifin_fu.
  qed.
  realize dK_ll.
  by rewrite /is_lossless MUnit.dunit_ll.
  qed.

  (* TODO move this *)
  lemma size_behead (l : 'a list) : l <> [] => size (behead l) = size l - 1.
  proof. by case l=> // ?? /=; ring. qed.

  local module (D': PRPt.Distinguisher) (P' : PRPt.Oracles) = {
    proc distinguish () : bool = {
      var b : bool;
      Redo.init();
      b <@ DRestr(D,SqueezelessSponge(P'),P').distinguish();
      return b;
    }
  }.

  local lemma DoubleBounding (P <: PRPt.StrongPRP {D, C, DBounder, Redo}) &m:
    Pr[PRPt.IND(P,D').main() @ &m: res]
    = Pr[PRPt.IND(P,DBounder(D')).main() @ &m: res].
  proof.
  byequiv=> //=; proc; inline *.
  wp.
  call (_: ={glob C, glob P, glob Redo}
    /\ all_prefixes Redo.prefixes{2}
    /\ Redo.prefixes{2}.[[]] = Some (b0,c0)
    /\ dom C.queries{2} <= dom Redo.prefixes{2}
    /\ prefixe_inv C.queries{2} Redo.prefixes{2}
    /\ DBounder.FBounder.c{2} = C.c{2}).
  + proc; sp; if=> //=; inline *.
    rcondt{2} 3; 1: by auto=> /#.
    by wp; call (_: true); auto.
  + proc; sp; if=> //=; inline *.
    rcondt{2} 3; 1: by auto=> /#.
    by wp; call (_: true); auto.
  + proc; sp; if=> //=; inline *;1:if;auto.

    splitwhile{1}5:take (i+1) p \in dom Redo.prefixes.
    splitwhile{2}5:take (i+1) p \in dom Redo.prefixes.

    alias{1}1 pref = Redo.prefixes;alias{2}1 pref = Redo.prefixes;sp 1 1=>/=.
    alias{1}1 query = C.queries;alias{2}1 query = C.queries;sp 1 1=>/=.
    conseq(:_==> ={sa, Redo.prefixes, glob P, i, C.c}
        /\ all_prefixes Redo.prefixes{2}
        /\ dom query{2} <= dom Redo.prefixes{2}
        /\ i{1} = size bs{1}
        /\ Redo.prefixes{1}.[take i{1} bs{1}] = Some (sa{1},sc{1})
        /\ (forall y, y \in dom pref{1} => pref{1}.[y] = Redo.prefixes{1}.[y])
        /\ (forall y, y \in dom Redo.prefixes{1} <=> (y \in dom pref{1} \/
             (exists j, 0 <= j <= i{1} /\ y = take j bs{1})))
        /\ DBounder.FBounder.c{2} = C.c{2} - size bs{1} + i{1});
    progress;..-2:smt(in_dom dom_set in_fsetU1 getP oget_some take_size cat_take_drop).
    while( ={sa, Redo.prefixes, glob P, i, C.c, p, sc} /\ p{1} = bs{1}
        /\ all_prefixes Redo.prefixes{2}
        /\ Redo.prefixes{2}.[[]] = Some (b0, c0)
        /\ dom query{2} <= dom Redo.prefixes{2}
        /\ (i{1} < size p{1} => ! take (i{1} + 1) p{1} \in dom Redo.prefixes{1})
        /\ 0 <= prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1}))) <= i{1} <= size bs{1}
        /\ C.c{1} <= max_size
        /\ Redo.prefixes{1}.[take i{1} bs{1}] = Some (sa{1},sc{1})
        /\ (forall y, y \in dom pref{1} => pref{1}.[y] = Redo.prefixes{1}.[y])
        /\ (forall y, y \in dom Redo.prefixes{1} <=> (y \in dom pref{1} \/
             (exists j, 0 <= j <= i{1} /\ y = take j bs{1})))
        /\ DBounder.FBounder.c{2} = C.c{2} - size bs{1} + i{1}).
    + if;auto;1:smt(get_oget in_dom).
      sp;rcondt{2}1;1:auto=>/#;auto;1:call(:true);auto;progress.
      * move=>x;rewrite dom_set in_fsetU1=>[][|-> j];1:smt(in_fsetU1).
        case(0 <= j)=>hj0;last first.
        + by rewrite (take_le0 j)1:/# in_fsetU1 in_dom H0//=.
        rewrite take_take in_fsetU1/min/#.
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * rewrite dom_set in_fsetU1 negb_or H9 negb_or/=negb_exists/=.
        cut htake:take (i{2} + 1) bs{1} = take (i{2} + 1) (take (i{2} + 1 + 1) bs{1});
          smt(take_take size_take).
      * rewrite/#.
      * rewrite/#.
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
      * smt(dom_set in_fsetU1 take_take in_dom getP oget_some).
    sp;
    conseq(:_==> ={sa, Redo.prefixes, glob P, i, C.c, p, sc, bs} /\ p{1} = bs{1}
        /\ Redo.prefixes{2} = pref{2}
        /\ dom query{2} <= dom Redo.prefixes{2}
        /\ C.c{1} <= max_size
        /\ i{1} = prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1})))
        /\ Redo.prefixes{1}.[take i{1} bs{1}] = Some (sa{1},sc{1})
        /\ DBounder.FBounder.c{2} = C.c{2} - size bs{1} +
               prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1}))));
       progress;..4,6..-2:
       smt(prefixe_ge0 prefixe_lt_size prefixe_sizel prefixe_exchange prefixe_lt_size memE). 
    + move:H8=>[]//=[]j [[hj0 hjsize] htake].
      rewrite htake. 
      apply take_get_max_prefixe2=>//=;1:smt(in_dom memE).
      by rewrite-(prefixe_exchange _ _ _ H2 H).
    alias{2} 1 k = DBounder.FBounder.c;sp;
    conseq(:_==> ={sa, Redo.prefixes, glob P, i, C.c, p, sc, bs} /\ p{1} = bs{1}
        /\ Redo.prefixes{2} = pref{2}
        /\ dom query{2} <= dom Redo.prefixes{2}
        /\ C.c{2} <= max_size
        /\ i{1} = prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1})))
        /\ Redo.prefixes{1}.[take i{1} bs{1}] = Some (sa{1},sc{1})
        /\ DBounder.FBounder.c{2} = k{2});1:progress=>/#.
    while( ={sa, Redo.prefixes, glob P, i, C.c, p, sc, bs, C.queries} /\ p{1} = bs{1}
        /\ Redo.prefixes{2} = pref{2}
        /\ dom query{2} <= dom Redo.prefixes{2}
        /\ prefixe_inv C.queries{2} Redo.prefixes{2}
        /\ all_prefixes Redo.prefixes{2}
        /\ C.c{2} <= max_size
        /\ 0 <= i{1} <= prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1})))
        /\ (forall j, 0 <= j <= prefixe bs{1} (get_max_prefixe bs{1} (elems (dom C.queries{1})))
            => take j bs{2} \in dom Redo.prefixes{1})
        /\ Redo.prefixes{1}.[take i{1} bs{1}] = Some (sa{1},sc{1})
        /\ DBounder.FBounder.c{2} = k{2}).
    + rcondt{1}1;2:rcondt{2}1;auto;progress.
      * by rewrite/#. 
      * by rewrite(prefixe_exchange _ _ bs{2} H0 H1)all_take_in//=/#.
      * smt(get_oget in_dom).
    auto;progress. smt(prefixe_ge0). 
    * apply take_get_max_prefixe2=>//=;1:smt(in_dom memE).
      by rewrite-(prefixe_exchange _ _ _ H2 H).
    * smt(get_oget in_dom).
    * smt(@Prefixe).
  auto;call(:true);auto;smt(dom0 in_fset0 dom_set in_fsetU1 getP oget_some).
  qed.

  local clone import ProdSampling with
    type t1 <- block,
      op d1 <- bdistr,
    type t2 <- capacity,
      op d2 <- cdistr.

  lemma Real_Concrete &m : 
    Pr[GReal(D).main()@ &m: res /\ C.c <= max_size] <=
    Pr[CF(DRestr(D)).main()@ &m: res] + (max_size ^ 2)%r / 2%r * mu dstate (pred1 witness).
  proof.
    cut->:
      Pr[RealIndif(SqueezelessSponge,PC(Perm),D).main()@ &m:
         res /\ C.c <= max_size] = Pr[GReal'.main()@ &m: res/\ C.c <= max_size].
    + byequiv=>//;proc;inline *;
      call (_: ={C.c, glob Perm, Redo.prefixes}
          /\ prefixe_inv C.queries{2} Redo.prefixes{1}
          /\ Redo.prefixes{1}.[[]] = Some (b0, c0)
          /\ all_prefixes Redo.prefixes{1});
        last first.
      + auto;smt(dom0 in_fset0 dom_set in_fsetU1 getP oget_some).
      + by proc; inline*; sp; if; auto. 
      + by proc; inline*; sp; if; auto. 
      proc; inline *; wp; sp.
      if{2};sp;wp;last first.
      + conseq(:_==> sa{1} = (oget Redo.prefixes{1}.[take i{1} p{1}]).`1
          /\ i{1} = size p{1}
          /\ Redo.prefixes{1}.[[]] = Some (b0, c0)
          /\ ={Perm.m, Perm.mi, Redo.prefixes, C.c});1:smt(take_size).
      + while{1}( ={Perm.m, Perm.mi, Redo.prefixes, C.c}
          /\ p{1} \in dom C.queries{2}
          /\ prefixe_inv C.queries{2} Redo.prefixes{1}
          /\ 0 <= i{1} <= size p{1}
          /\ Redo.prefixes{1}.[[]] = Some (b0, c0)
          /\ (sa{1},sc{1}) = oget Redo.prefixes{1}.[take i{1} p{1}]
          /\ all_prefixes Redo.prefixes{1})(size p{1} - i{1}).
        + auto;sp;rcondt 1;auto;smt(excepted_lossless).
        by auto;smt(size_ge0 take0 take_size).

      splitwhile{1} 1 : take (i+1) p \in dom Redo.prefixes;
      splitwhile{2} 1 : take (i+1) p \in dom Redo.prefixes.

      alias{1}1 pref = Redo.prefixes;alias{2}1 pref = Redo.prefixes;sp 1 1=>/=.
      alias{2}1 query = C.queries;sp 0 1=>/=.

      conseq(:_==> ={sa,Perm.m,Perm.mi,Redo.prefixes,i,p}
          /\ C.c{1} = C.c{2} - size p{2} + i{2}
          /\ i{2} = size p{2}
          /\ Redo.prefixes{2}.[take i{2} p{2}] = Some (sa{2}, sc{2})
          /\ (forall l, l \in dom pref{2} => pref{2}.[l] = Redo.prefixes{2}.[l])
          /\ (forall j, 0 <= j <= i{2} => take j p{2} \in dom Redo.prefixes{2})
          /\ (forall l, l \in dom Redo.prefixes{2} => 
               l \in dom pref{2} \/ (exists j, 0 <= j <= i{2} /\ l = take j p{2})));
        progress.
      * by rewrite/#.
      * move:H3 H7;rewrite take_size dom_set in_fsetU1 getP;case(bs0 = bs{2})=>//=[->|]h.
        * by rewrite h oget_some/=.
        * move:H=>[?[??]];move=>? ?.
          by rewrite -H4;1:smt(take_size);rewrite H//=.
      * smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0).
      * smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop).
      * smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop).
      * smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop).
      while(={sa,sc,Perm.m,Perm.mi,Redo.prefixes,i,p}
          /\ C.c{1} = C.c{2} - size p{2} + i{2}
          /\ all_prefixes Redo.prefixes{2}
          /\ all_prefixes pref{2}
          /\ prefixe_inv C.queries{2} pref{2}
          /\ prefixe p{2} (get_max_prefixe p{2} (elems (dom C.queries{2}))) <= i{2} <= size p{2}
          /\ Redo.prefixes{2}.[take i{2} p{2}] = Some (sa{2}, sc{2})
          /\ (forall l, l \in dom pref{2} => pref{2}.[l] = Redo.prefixes{2}.[l])
          /\ (forall j, 0 <= j <= i{2} => take j p{2} \in dom Redo.prefixes{2})
          /\ (forall l, l \in dom Redo.prefixes{2} => 
               l \in dom pref{2} \/ (exists j, 0 <= j <= i{2} /\ l = take j p{2}))).
      + rcondf{1}1;2:rcondf{2}1;..2:auto;progress.
         * cut:=H7 (take (i{m0}+1) p{m0}).
           case((take (i{m0} + 1) p{m0} \in dom Redo.prefixes{m0}))=>//=_.
           rewrite negb_or negb_exists/=;progress. 
           + by rewrite memE prefixe_lt_size//=-(prefixe_exchange _ _ p{m0} H1 H0)//=/#.
           case(0<=a<=i{m0})=>//=ha;smt(size_take).
         * cut:=H7 (take (i{hr}+1) p{hr}).
           case((take (i{hr} + 1) p{hr} \in dom Redo.prefixes{hr}))=>//=_.
           rewrite negb_or negb_exists/=;progress. 
           + by rewrite memE prefixe_lt_size//=-(prefixe_exchange _ _ p{hr} H1 H0)//=/#.
           case(0<=a<=i{hr})=>//=ha;smt(size_take).
           
        sp;auto;if;auto;progress.
        * rewrite/#.
        * move=>x;rewrite dom_set in_fsetU1=>[][|h];1:
            smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop).
          rewrite h=>j;rewrite take_take in_fsetU1/min.
          case(j<i{2}+1)=>//=hij.
          cut->:take j p{2} = take j (take i{2} p{2});smt(take_take take_le0).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * rewrite!getP/=.
          cut/#: !take (i{2} + 1) p{2} \in dom pref{2}.
          by rewrite memE prefixe_lt_size//=-(prefixe_exchange _ _ _ H1 H0)//=/#.
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * move=>x;rewrite dom_set in_fsetU1=>[][|h];1:
            smt(dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop).
          rewrite h=>j;rewrite take_take in_fsetU1/min.
          case(j<i{2}+1)=>//=hij.
          cut->:take j p{2} = take j (take i{2} p{2});smt(take_take take_le0).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * rewrite!getP/=.
          cut/#: !take (i{2} + 1) p{2} \in dom pref{2}.
          by rewrite memE prefixe_lt_size//=-(prefixe_exchange _ _ _ H1 H0)//=/#.
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
        * smt(prefixe_lt_size  dom_set in_fsetU1 take_size oget_some getP in_dom take_oversize take_le0 take_take cat_take_drop memE).
      conseq(:_==> ={sa,sc,Perm.m,Perm.mi,Redo.prefixes,i,p}
          /\ C.c{1} = C.c{2} - size p{2} + i{2}
          /\ pref{2} = Redo.prefixes{2}
          /\ all_prefixes pref{2}
          /\ prefixe_inv C.queries{2} pref{2}
          /\ prefixe p{2} (get_max_prefixe p{2} (elems (dom C.queries{2}))) = i{2}
          /\ Redo.prefixes{2}.[take i{2} p{2}] = Some (sa{2}, sc{2}));1:
        smt(prefixe_sizel take_get_max_prefixe2 in_dom prefixe_exchange).

      while( ={sa,sc,Perm.m,Perm.mi,Redo.prefixes,i,p}
          /\ C.c{1} = C.c{2} - size p{2} + prefixe p{2} (get_max_prefixe p{2} (elems (dom C.queries{2})))
          /\ pref{2} = Redo.prefixes{2}
          /\ all_prefixes pref{2}
          /\ prefixe_inv C.queries{2} pref{2}
          /\ 0 <= i{2} <= prefixe p{2} (get_max_prefixe p{2} (elems (dom C.queries{2})))
          /\ Redo.prefixes{2}.[take i{2} p{2}] = Some (sa{2}, sc{2})).
      + rcondt{1}1;2:rcondt{2}1;auto;progress.
        * rewrite/#. search get_max_prefixe (<=) take mem.
        * rewrite(prefixe_inv_leq _ _ _ _ _ _ H H7 H0)//= 1:/#.
          cut:=H0=>[][h1 [h2 h3]].
          cut:=h3 _ _ _ H7;last smt(memE).
          smt(size_eq0 size_take).
        * smt(get_oget in_dom).
      auto;progress.
      * rewrite/#.
      * smt(prefixe_ge0).
      * smt(take0).
      * smt(prefixe_sizel @Prefixe memE).
      * smt(prefixe_sizel @Prefixe memE).

    have p_ll := P_f_ll _ _.
    + apply/dprod_ll; split.
      + exact/Block.DBlock.dunifin_ll.
      exact/Capacity.DCapacity.dunifin_ll.
    + apply/fun_ext=>- [] a b; rewrite supp_dprod.
      by rewrite/=/predT/=Block.DBlock.dunifin_fu Capacity.DCapacity.dunifin_fu. 
    have pi_ll := P_fi_ll _ _.
    + apply/dprod_ll; split.
      + exact/Block.DBlock.dunifin_ll.
      exact/Capacity.DCapacity.dunifin_ll.
    + apply/fun_ext=>- [] a b; rewrite supp_dprod.
      by rewrite/=/predT/=Block.DBlock.dunifin_fu Capacity.DCapacity.dunifin_fu. 
    have f_ll : islossless SqueezelessSponge(Perm).f.
    + proc; while true (size p - i)=> //=.
      * move=> z; wp;if;auto; 2:call p_ll; auto=>/#. 
      by auto; smt w=size_ge0.
    apply (ler_trans _ _ _ 
             (Pr_restr Perm SqueezelessSponge D p_ll pi_ll f_ll D_ll &m)).
    have ->: Pr[Indif(SqueezelessSponge(Perm), Perm, DRestr(D)).main() @ &m: res]
             = Pr[PRPt.IND(PRPi.PRPi,DBounder(D')).main() @ &m: res].
    + rewrite -(DoubleBounding PRPi.PRPi &m).
      byequiv=> //=; proc; inline *; sim (_: ={m,mi}(Perm,PRPi.PRPi) /\ ={glob C}).
      * by proc; if=> //=; auto.
      by proc; if=> //=; auto.
    have ->: Pr[CF(DRestr(D)).main() @ &m: res]
             = Pr[PRPt.IND(ARP,DBounder(D')).main() @ &m: res].
    + rewrite -(DoubleBounding ARP &m).
      byequiv=> //=; proc; inline *; sim (_: ={m,mi}(PF,ARP)).
      * proc; if=> //=; auto; conseq (_: true ==> (y1,y2){1} = x{2})=> //=.
        transitivity{1} { (y1,y2) <@ S.sample2(); }
                        (true ==> ={y1,y2})
                        (true ==> (y1,y2){1} = x{2})=> //=.
        - by inline *; auto.
        transitivity{2} { x <@ S.sample(); }
                        (true ==> (y1,y2){1} = x{2})
                        (true ==> ={x})=> //=.
        - by symmetry; call sample_sample2; skip=> /> [].
        by inline *; auto.
      proc; if=> //=; auto; conseq (_: true ==> (y1,y2){1} = y{2})=> //=.
      transitivity{1} { (y1,y2) <@ S.sample2(); }
                      (true ==> ={y1,y2})
                      (true ==> (y1,y2){1} = y{2})=> //=.
      - by inline *; auto.
      transitivity{2} { y <@ S.sample(); }
                      (true ==> (y1,y2){1} = y{2})
                      (true ==> ={y})=> //=.
      - by symmetry; call sample_sample2; skip=> /> [].
      by inline *; auto.
    have /#:= Conclusion D' &m _.
    move=> O O_f_ll O_fi_ll.
    proc;inline*;sp;wp; call (_: true)=> //=. 
    + apply D_ll.
    + by proc; inline*; sp; if=> //=; auto; call O_f_ll; auto.
    + by proc; inline*; sp; if=> //=; auto; call O_fi_ll; auto.
    + proc; inline *; sp; if=> //=; auto; if; auto.
      while true (size p - i);auto.
      * sp; if; auto; 2:call O_f_ll; auto=> /#.
      by auto; smt w=size_ge0.
  qed.

end section.
